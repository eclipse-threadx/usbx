cmake_minimum_required(VERSION 3.13 FATAL_ERROR)

project(libs LANGUAGES C)

if($ENV{ENABLE_64})
  message(STATUS "Building for 64bit")
  set(NX_USER_FILE ${CMAKE_CURRENT_SOURCE_DIR}/nx_user.h)
else()
  add_compile_options(-m32)
  add_link_options(-m32)
  message(STATUS "Building for 32bit")
endif()
message(STATUS "Using toolchain file: ${CMAKE_TOOLCHAIN_FILE}.")

get_filename_component(externals ${CMAKE_CURRENT_SOURCE_DIR}/../../../../externals
                       ABSOLUTE)
add_subdirectory(${externals}/threadx threadx)
add_subdirectory(${externals}/netx/netxduo netxduo)
add_subdirectory(${externals}/filex filex)
target_compile_options(threadx PRIVATE -DTX_ENABLE_EVENT_TRACE)
if(NOT DEFINED ENV{ENABLE_IDLE})
  target_compile_options(threadx PRIVATE -DTX_LINUX_NO_IDLE_ENABLE)
endif()
target_compile_options(filex PRIVATE -DFX_ENABLE_EXFAT)


# Replace addon files from netxduo
get_target_property(SOURCES_LIST netxduo SOURCES)
target_compile_options(netxduo PRIVATE -DTX_ENABLE_EVENT_TRACE -DNX_PHYSICAL_HEADER=20)
set(NEW_SOURCES_LIST "")
foreach(SOURCE ${SOURCES_LIST})
  if(SOURCE MATCHES ".*azure_iot.*")
    list(APPEND NEW_SOURCES_LIST ${SOURCE})
  else()
    string(REPLACE "netxduo/addons" "addons" NEW_SOURCE ${SOURCE})
    list(APPEND NEW_SOURCES_LIST ${NEW_SOURCE})
  endif()
endforeach()
set_target_properties(netxduo PROPERTIES SOURCES "${NEW_SOURCES_LIST}")
get_target_property(INCLUDE_LIST netxduo INCLUDE_DIRECTORIES)
set(NEW_INCLUDE_LIST "")
foreach(INCLUDE ${INCLUDE_LIST})
  if(INCLUDE MATCHES ".*azure_iot.*")
    list(APPEND NEW_INCLUDE_LIST ${INCLUDE})
  else()
    string(REPLACE "netxduo/addons" "addons" NEW_INCLUDE ${INCLUDE})
    list(APPEND NEW_INCLUDE_LIST ${NEW_INCLUDE})
  endif()
endforeach()
set_target_properties(netxduo PROPERTIES INCLUDE_DIRECTORIES "${NEW_INCLUDE_LIST}")
set_target_properties(netxduo PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${NEW_INCLUDE_LIST}")

foreach(lib threadx netxduo filex)
  get_target_property(dirs ${lib} INCLUDE_DIRECTORIES)
  execute_process(COMMAND mkdir -p ${CMAKE_BINARY_DIR}/inc)
  foreach(dir ${dirs})
    file(GLOB header_files ${dir}/*.h)
    foreach(header_file ${header_files})
      execute_process(COMMAND ln -sf ${header_file} ${CMAKE_BINARY_DIR}/inc)
    endforeach()
  endforeach()
endforeach()
